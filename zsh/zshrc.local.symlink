# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# use .zshrc.private for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.zshrc.private ]]
then
  source ~/.zshrc.private
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

## handle ssh agent
[[ -x $ZSH/bin/ssh-agent-handling.sh ]] && $ZSH/bin/ssh-agent-handling.sh

## path handling
[[ -d "$HOME/bin" ]] && PATH="$HOME/bin:$PATH"
[[ -d "$HOME/.dotfiles/bin" ]] && PATH="$HOME/.dotfiles/bin:$PATH"
[[ -d "$HOME/.private_dotfiles/bin" ]] && PATH="$HOME/.private_dotfiles/bin:$PATH"
