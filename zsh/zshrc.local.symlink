# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# use .zshrc.private for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.zshrc.private ]]
then
  source ~/.zshrc.private
fi

# load syntx highlight plugin first
source $ZSH/zsh/plugins/zsh-syntax-highlighting.zsh

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

## handle ssh agent
if ! [ -x "$SSH_AUTH_SOCK" ]; then
  eval $($HOME/.dotfiles/bin/keychain --eval -q)
fi

### path handling
[[ -d "$HOME/bin" ]] && PATH="$HOME/bin:$PATH"
[[ -d "$HOME/.dotfiles/bin" ]] && PATH="$HOME/.dotfiles/bin:$PATH"
[[ -d "$HOME/.private_dotfiles/bin" ]] && PATH="$HOME/.private_dotfiles/bin:$PATH"

### use 256 colors
if [[ "$TERM" == xterm ]]; then
  export TERM=xterm-256color
elif [[ "$TERM" == screen ]]; then
  export TERM=screen-256color
fi

### syntax highlighter
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern)

## history search plugin
zmodload zsh/terminfo
bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down

